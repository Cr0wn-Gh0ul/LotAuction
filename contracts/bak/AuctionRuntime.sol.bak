// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

import "@openzeppelin/contracts/finance/PaymentSplitter.sol";
import "./Constants.sol";
import "./RedBlackTreeLibrary.sol";

contract AuctionRuntime {
    using RedBlackTreeLibrary for RedBlackTreeLibrary.Tree;

    RedBlackTreeLibrary.Tree bidTree;
    
    struct AuctionData {
        uint256 maxWinningBids;
        uint256 winningBidsPlaced;
        uint256 endTime;
        uint256 reservePrice;
        uint256 minBidIncrement;
        uint256 totalValue;
        uint256 lotSize;
    }
    AuctionData public auction;

    mapping(address => uint256) addressToAmount;
    mapping(uint256 => address) amountToAddress;

    bool public isPaused;
    address private _auctionOwner;

    modifier onlyAuctionOwner() {
        require(
            msg.sender == _auctionOwner,
            "Only auction owner can call this function"
        );
        _;
    }

    modifier isAuctionActive() {
        require(block.number <= auction.endTime, "Auction has ended");
        _;
    }

    modifier hasActiveBid() {
        require(addressToAmount[msg.sender] > 0, "No bid exists");
        _;
    }

    modifier notPaused() {
        require(!isPaused, "Contract is paused");
        _;
    }

    constructor(AuctionData memory auction_, address auctionOwner_) {
        auction = auction_;
        _auctionOwner = auctionOwner_;
    }

    function destroyAuction(address settler_) external onlyAuctionOwner {
        uint256 nintyPercent = (address(this).balance * 90) / 100;
        (bool sent, ) = _auctionOwner.call{value: nintyPercent}("");
        require(sent, "Withdraw failed");
        selfdestruct(payable(settler_));
    }

    function togglePaused() external onlyAuctionOwner {
        isPaused = !isPaused;
    }

    function addBid() public payable isAuctionActive {
        AuctionData memory auction_ = auction;
        bool isReplacement = _validateBid(auction_, msg.sender, msg.value);
        // if number of winning bids is equal to max number of winning bids
        if (isReplacement) {
            uint256 lowestBid = bidTree.first();
            _removeBid(amountToAddress[lowestBid], lowestBid);
        } else {
            // increment number of winning bids
            auction_.winningBidsPlaced++;
        }
        // add bid to active bid mapping
        addressToAmount[msg.sender] = msg.value;
        // for reverse tree lookup
        amountToAddress[msg.value] = msg.sender;
        // add bid to bid tree
        bidTree.insert(msg.value);
        auction_.totalValue += msg.value;
        auction = auction_;
    }

    function removeBid() external isAuctionActive hasActiveBid {
        auction.winningBidsPlaced--;
        _removeBid(msg.sender, addressToAmount[msg.sender]);
    }

    function emergencyRelease() external hasActiveBid {
        require(isPaused, "Contract is not paused");
        auction.winningBidsPlaced--;
        _removeBid(msg.sender, addressToAmount[msg.sender]);
    }

    function _removeBid(address user_, uint256 amount_) internal {
        delete addressToAmount[user_];
        delete amountToAddress[amount_];
        bidTree.remove(amount_);
        bool success = _refundBid(user_, amount_);
        if (success) {
            auction.totalValue -= amount_;
        }
    }

    function increaseBid() external payable isAuctionActive hasActiveBid {
        AuctionData memory auction_ = auction;
        uint256 lastBid = addressToAmount[msg.sender];
        uint256 newBid = (lastBid + msg.value);
        require(
            newBid > lastBid + ((lastBid * auction_.minBidIncrement) / 100),
            "New bid is lower than last bid plus minimum bid increment"
        );
        bidTree.remove(lastBid);
        auction_.totalValue += msg.value;
        addressToAmount[msg.sender] = newBid;
        amountToAddress[newBid] = msg.sender;
        bidTree.insert(newBid);
        auction = auction_;
    }

    function validateBid(address address_, uint256 amount_)
        public
        view
        returns (bool isReplacement)
    {
        AuctionData memory a = auction;
        return _validateBid(a, address_, amount_);
    }

    function _validateBid(
        AuctionData memory auction_,
        address address_,
        uint256 amount_
    ) internal view returns (bool isReplacement) {
        preCheckBid(auction_, address_, amount_);
        return isReplacementBid(auction_, amount_);
    }

    function preCheckBid(
        AuctionData memory auction_,
        address addr_,
        uint256 amount_
    ) internal view isAuctionActive {
        // check if bid already exists
        require(addressToAmount[addr_] == 0, "Bidder already bid");
        // chheck if bid is greater than reserve price
        require(
            amount_ >= auction_.reservePrice,
            "Bid amount is less than reserve price"
        );
        // check if bid for this amount already exists
        require(!bidTree.exists(amount_), "Bid amount already exists");
    }

    function isReplacementBid(AuctionData memory auction_, uint256 value_)
        internal
        view
        returns (bool)
    {
        // see if we are replacing a bid
        if (auction_.winningBidsPlaced >= auction_.maxWinningBids) {
            // find lowest winning bid
            uint256 lowestBid = bidTree.first();
            // require bid is greater than lowest winning bid
            require(
                value_ >=
                    lowestBid + ((lowestBid * auction_.minBidIncrement) / 100),
                "Bid amount is less than the lowest winning bid + minBidIncrement"
            );
            // return the lowest winning bid
            return true;
        }
        // max number of winning bids not reached
        return false;
    }

    /**
     * @dev Returns the winners of an auction
     */
    function winnersOfAuction() external view returns (address[] memory) {
        AuctionData memory auction_ = auction;
        // create an array of addresses to store winners
        address[] memory winners = new address[](auction_.winningBidsPlaced);
        // get the first bid in the bid tree
        uint256 currentValue = bidTree.first();
        for (uint256 i = 0; i < auction_.winningBidsPlaced; i++) {
            // add the address of the bid from the bid tree to the winners array
            winners[i] = amountToAddress[currentValue];
            // get the next bid in the bid tree
            currentValue = bidTree.next(currentValue);
        }
        // return the winners array
        return winners;
    }

    /**
     * @dev Refunds a bid and removes the bid from the active bid mapping
     */
    function _refundBid(address to_, uint256 amount_) internal returns (bool) {
        // refund bid
        (bool success, ) = to_.call{gas: 30000, value: amount_}("");
        return success;
    }
}
